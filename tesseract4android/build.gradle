plugins {
	id 'com.android.library'
	id 'maven-publish'
}

android {
	namespace 'cz.adaptech.tesseract4android'
	compileSdk 33
	ndkVersion rootProject.ext.ndkVersion

	defaultConfig {
		minSdk 16
		targetSdk 33

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
		externalNativeBuild {
			cmake {
				// Specifies which native libraries or executables to build and package.
				// TODO: Include eyes-two in some build flavor of the library?
				//targets "jpeg", "pngx", "leptonica", "tesseract"
				cppFlags ''
				arguments "-DANDROID_STL=c++_shared"
			}
		}
		ndk {
			// Specify the ABI configurations that Gradle should build and package.
			// By default it compiles all available ABIs.
			//abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
		}
	}
	externalNativeBuild {
		cmake {
			path "src/main/cpp/CMakeLists.txt"
			version rootProject.ext.cmakeVersion
		}
	}
	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
		debug {
			externalNativeBuild {
				cmake {
					// Force building release version of native libraries even in debug variant.
					// This is for projects that has direct dependency on this library,
					// but doesn't really want its debug version, which is very slow.
					// Note that this only affects native code.
					arguments "-DCMAKE_BUILD_TYPE=Release"
				}
			}
		}
	}
	flavorDimensions = ["parallelization"]
	productFlavors {
		standard {
		}
		openmp {
			externalNativeBuild {
				// NOTE: This must be also here in tesseract4android module for some reason.
				// Otherwise it uses the openmp variant of tesseract for both standard and openmp
				// variants in here.
				cmake {
					// NOTE: We must add -static-openmp argument to build it statically,
					// because shared library is not being included in the resulting APK.
					// See: https://github.com/android/ndk/issues/1028
					// Use of that argument shows warnings during build:
					// > C/C++: clang: warning: argument unused during compilation: '-static-openmp' [-Wunused-command-line-argument]
					// But it has no effect on the result.
					cFlags "-fopenmp -static-openmp -Wno-unused-command-line-argument"
					cppFlags "-fopenmp -static-openmp -Wno-unused-command-line-argument"
				}
			}
		}
	}
	publishing {
		singleVariant("standardRelease") {
			withSourcesJar()
			withJavadocJar()
		}
		singleVariant("openmpRelease") {
			withSourcesJar()
			withJavadocJar()
		}
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_17
		targetCompatibility JavaVersion.VERSION_17
	}
	buildFeatures {
		buildConfig true
		prefab true
	}
}

dependencies {
	// Intentionally use old version of annotation library which doesn't depend on kotlin-stdlib
	// to not unnecessarily complicate client projects due to potential duplicate class build errors
	// caused by https://kotlinlang.org/docs/whatsnew18.html#updated-jvm-compilation-target
	//noinspection GradleDependency
	implementation 'androidx.annotation:annotation:1.3.0'

	// We use `compileOnly`, because we use it only as prefab dependency for CMake
	// In case we would use `implementation` the project consuming this library would have conflict
	// with same .so files being included from both tesseract4android and leptonica / tesseract.
	compileOnly project(':leptonica')
	compileOnly project(':tesseract')

	testImplementation 'junit:junit:4.13.2'
	androidTestImplementation 'androidx.test:runner:1.5.2'
	androidTestImplementation 'androidx.test:rules:1.5.0'
	androidTestImplementation 'androidx.test.ext:junit:1.1.5'
	androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

afterEvaluate {
	publishing {
		publications {
			standard(MavenPublication) {
				from components.findByName('standardRelease')

				groupId 'cz.adaptech'
				artifactId 'tesseract4android'
				version rootProject.ext.tesseract4AndroidVersion
			}
			openmp(MavenPublication) {
				from components.findByName('openmpRelease')

				groupId 'cz.adaptech'
				artifactId 'tesseract4android-openmp'
				version rootProject.ext.tesseract4AndroidVersion
			}
		}
	}
}

// Workaround for errors caused by building modules in parallel
// See https://issuetracker.google.com/issues/265544858#comment22
afterEvaluate {
	android.libraryVariants.configureEach { variant ->
		variant.preBuildProvider.configure {
			// Add all the project dependencies here
			// NOTE: Leptonica doesn't use any flavor, so use just name of buildType
			dependsOn.add(":leptonica:prefab${variant.buildType.name.capitalize()}ConfigurePackage")
			dependsOn.add(":tesseract:prefab${variant.name.capitalize()}ConfigurePackage")
		}
	}
}
